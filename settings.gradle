rootProject.name = '10-billion-per-month'

["cloud", "boot", "data"].each {

    def projectDir = new File(rootDir, it)
    if(!projectDir.exists()){
        projectDir.mkdirs()
    }

    projectDir.eachDir {subDir ->
        def gradleFile = new File(subDir.absolutePath, "build.gradle")
        if(!gradleFile.exists()){
            gradleFile.text =
                    """
                    
                    dependencies {
                
                    }
                
                    """.stripIndent(20)
        }
        [
                "src/main/java/com/example/dev",
                "src/main/resources",
                "src/test/java/com/example/dev",
                "src/test/resources"
        ].each {srcDir->
            def srcFolder = new File(subDir.absolutePath, srcDir)
            if(!srcFolder.exists()){
                srcFolder.mkdirs()
            }
        }

        def projectName = ":${it}-${subDir.name}";
        include projectName
        project(projectName).projectDir = subDir
    }
}